c africa_iso_6438_3.kmn
c 
c Africa ISO 4438 (Version 3)
c Unicode Normalization Form: NFD
c 
c v1: Original keyboard layout by Kashorow. Using AltGr key sequences to type extended Latin characters and diacritics
c v2: Keyboard layout by Kashorow, using deadkeys to type extended Latin characters and diacritics
c v3: Modifications to v.2 keyboard layout based on keyboard logic and documentation developed for the Open Road project.
c v4:
c 
c 
c Andrew Cunningham    2008-08-06
c andrewc AT vicnet.net.au
c lang.support AT gmail.com

store(&Version) "6.0"
c store(&Bitmap) ""
store(&Copyright) "© 2008 Kasahorow"
store(&Message) "Keyboard layout designed for ISO 6438 support by Kasahorow."
store(&Name) "Africa ISO 6438"

begin Unicode > use(main)

c Diacritics with combining class 220 (below base charater)
store(cc220_Key) U+002A U+003A U+005F
store(cc220_Char) U+0323 U+0324 U+0331

c Diacritics with combining class 230 (above base character)
store(cc230_Key) U+0060 U+0027 U+0036 U+007E U+002D U+0039 U+0038 U+0022 U+0030
store(cc230_Char) U+0300 U+0301 U+0302 U+0303 U+0304 U+0306 U+0307 U+0308 U+030C  

c Extended Latin characters
store(extChar_key_lower) "abcdefghijklmnopqrstuvwxyz[]"
store(extChar_key_upper) "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
store(extChar_char_lower) U+0263 U+0253 U+0188 U+0257 U+0259 U+0192 U+0260 U+0127 U+026a U+0288 U+0199 U+0292 U+0271 U+014b U+0275 U+01a5 U+025b U+027d U+0256 U+01ad U+028a U+028b U+0254 U+03c7 U+01b4 U+0283 U+0298 U+01c1
store(extChar_char_upper) U+0194 U+0181 U+0187 U+018a U+018e U+0191 U+0193 U+0126 U+0197 U+01ae U+0198 U+01b7 U+0271 U+014A U+019F U+01a4 U+0190 U+027d U+0189 U+01ac U+01b1 U+01b2 U+0186 U+03a7 U+01b3 U+01a9

c do not work with deadkey
store(nonBase) "123457=!@#$%^&()+{[]}|;,<.>/?"

c all diacritics
store(diacriticKey) outs(cc220_Key) outs(cc230_Key)
store(diacriticChar) outs(cc220_Char) outs(cc230_Char)

c all extended Latin characters and diacritics
store(allExt_keys) outs(extChar_key_lower) outs(extChar_key_upper) outs(cc220_Key) outs(cc230_Key)
store(allExt_chars) outs(extChar_char_lower) outs(extChar_char_upper) outs(cc220_Char) outs(cc230_Char)

group(main) using keys

+ "\" > dk(1) 
dk(1) + "\" > "\"
dk(1) + any(nonBase) > context beep

c suppress invalid key sequences for extended latin

c limit base characters that support multiple diacritics to maximum of two combining class 230 diacritics
any(cc230_Char) any(cc230_Char) dk(1) + any(cc230_Key) > context(1) context(2) beep
any(cc220_Char) any(cc230_Char) any(cc230_Char) dk(1) + any(cc230_Key) > context(1) context(2) context(3) beep

c limit base characters that support multiple diacritics to a single combining class 220 diacritic
any(cc220_Char) dk(1) + any(cc220_Key) > context(1) beep
any(cc220_Char) any(cc230_Char) dk(1) + any(cc220_Key) > context(1) context(2) beep
any(cc220_Char) any(cc230_Char) any(cc230_Char) dk(1) + any(cc220_Key) > context(1) context(2) context(3) beep

c prevent entering double diacritics
U+0323 dk(1) + "*" > context(1) beep
U+0324 dk(1) + ":" > context(1) beep
U+0331 dk(1) + "_" > context(1) beep
U+0300 dk(1) + "`" > context(1) beep  
U+0301 dk(1) + "'" > context(1) beep  
U+0302 dk(1) + "6" > context(1) beep  
U+0303 dk(1) + "~" > context(1) beep  
U+0304 dk(1) + "-" > context(1) beep  
U+0306 dk(1) + "9" > context(1) beep  
U+0307 dk(1) + "8" > context(1) beep  
U+0308 dk(1) + '"' > context(1) beep
U+030C dk(1) + "0" > context(1) beep  

c rules for processing extended latin characters

dk(1) + any(allExt_keys) > index(allExt_chars,2) use(reorder_diacritics)

group(reorder_diacritics)
any(cc230_Char) any(cc220_Char) > context(2) Context(1)
any(cc230_Char) any(cc230_Char) any(cc220_Char) > context(3) context(1) context(2) 

c EOF
